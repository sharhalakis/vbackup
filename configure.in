#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT([sbackup], [0.1.0], [v13@v13.gr])
AM_INIT_AUTOMAKE(sbackup, 0.1.0)

AC_ARG_WITH( tar,
	[AC_HELP_STRING([--with-tar=PATH],
		[Full path to GNU tar])	],
	[GTAR="$with_tar"])

# Checks for programs.

# Check for GNU tar
if test -z "$GTAR" ; then
	AC_PATH_PROG([GTAR], [tar], [])
fi

AC_CACHE_CHECK(
	[whether we have GNU tar],
	[sbackup_cv_gtar],
	[
	 	if test -z "$GTAR" ; then
			sbackup_cv_gtar="-"
		else
			T=`$GTAR --version 2>&1 |grep 'GNU'`
			if test -z "$T" ; then
				GTAR=""
				sbackup_cv_gtar="no"
			else
				sbackup_cv_gtar="yes"
			fi
		fi
	])

if ! test "$sbackup_cv_gtar" = "yes" ; then
	GTAR=""
fi

# Check for xfsdump
AC_PATH_PROG([XFSDUMP], [xfsdump], [], [$PATH:/sbin:/usr/sbin:/usr/local/sbin])

# Checks for libraries.

# Checks for header files.

# Checks for typedefs, structures, and compiler characteristics.

AC_SUBST([mybindir], [${datadir}/sbackup/bin])
AC_SUBST([myhelperdir], [${datadir}/sbackup/helpers])
AC_SUBST([myscriptdir], [${datadir}/sbackup/scripts])
AC_SUBST([myconfdir], [${sysconfdir}/sbackup])
dnl AC_SUBST([sampledir], [${myconfdir}/samples])
AC_SUBST([sampledir], [${docdir}/samples])

AC_SUBST([GTAR])
AC_SUBST([XFSDUMP])

# Checks for library functions.
AC_CONFIG_FILES([Makefile
		scripts/Makefile
		scripts/bin/Makefile
		scripts/bin/run
		scripts/bin/sbackup
		scripts/bin/common
		scripts/scripts/Makefile
		scripts/scripts/mount/Makefile
		scripts/scripts/db/Makefile
		scripts/scripts/misc/Makefile
		scripts/scripts/fs/Makefile
		])
AC_OUTPUT

ERR=0
if test -z "$GTAR" ; then
	echo 
	echo " * No GNU tar was found. The tar method will not function."
	ERR=1
fi

if test -z "$XFSDUMP" ; then
	echo 
	echo " * xfsdump was not found. The xfsdump method will not function."
	ERR=1
fi

test "x$ERR" = "x1" && echo

